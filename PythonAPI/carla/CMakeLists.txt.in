cmake_minimum_required (
  VERSION
  @CARLA_CMAKE_MINIMUM_REQUIRED_VERSION@
)

project (
  ${SKBUILD_PROJECT_NAME}
  VERSION @CARLA_VERSION@
  LANGUAGES CXX
)

cmake_policy (SET CMP0097 NEW)
cmake_policy (SET CMP0091 NEW)
cmake_policy (SET CMP0074 NEW)
cmake_policy (SET CMP0077 NEW)
cmake_policy (SET CMP0117 NEW)
if (${CMAKE_MINOR_VERSION} GREATER_EQUAL 24)
  cmake_policy (SET CMP0135 NEW)
endif ()

include (@CARLA_WORKSPACE_PATH@/CMake/Util.cmake)

set (CMAKE_CXX_STANDARD 20)
set (CMAKE_CXX_STANDARD_REQUIRED ON)

set (CMAKE_C_STANDARD 11)
set (CMAKE_C_STANDARD_REQUIRED ON)

find_package (
  Python3
  COMPONENTS
    Interpreter
    Development.Module
    Development.Embed
    Development.SABIModule
  REQUIRED
)

if (WIN32)
  set (PYTHON_MODULE_EXT .pyd)
else ()
  set (PYTHON_MODULE_EXT .so)
endif ()

set (
  CARLA_PYTHON_API_CARLA_PATH
  ${CMAKE_CURRENT_SOURCE_DIR}
)

carla_message_verbose (
  "Selected Python executable: ${Python3_EXECUTABLE} (Interpreter ID: ${Python3_INTERPRETER_ID})"
)

set (
  CARLA_PYTHON_API_DEPENDENCIES
)

set (
  CARLA_PYTHON_API_INCLUDE_PATH
  ${CARLA_PYTHON_API_CARLA_PATH}/include
)

set (
  CARLA_PYTHON_API_SOURCE_PATH
  ${CARLA_PYTHON_API_CARLA_PATH}/src
)

set (
  PYTHON_API_HEADERS
  ${CARLA_PYTHON_API_INCLUDE_PATH}/PythonAPI.h
)

set (
  PYTHON_API_SOURCES
  ${CARLA_PYTHON_API_SOURCE_PATH}/PythonAPI.cpp
  ${CARLA_PYTHON_API_SOURCE_PATH}/Actor.cpp
  ${CARLA_PYTHON_API_SOURCE_PATH}/Blueprint.cpp
  ${CARLA_PYTHON_API_SOURCE_PATH}/Client.cpp
  ${CARLA_PYTHON_API_SOURCE_PATH}/Commands.cpp
  ${CARLA_PYTHON_API_SOURCE_PATH}/Control.cpp
  ${CARLA_PYTHON_API_SOURCE_PATH}/Exception.cpp
  ${CARLA_PYTHON_API_SOURCE_PATH}/Geometry.cpp
  ${CARLA_PYTHON_API_SOURCE_PATH}/LightManager.cpp
  ${CARLA_PYTHON_API_SOURCE_PATH}/Map.cpp
  ${CARLA_PYTHON_API_SOURCE_PATH}/Sensor.cpp
  ${CARLA_PYTHON_API_SOURCE_PATH}/SensorData.cpp
  ${CARLA_PYTHON_API_SOURCE_PATH}/Snapshot.cpp
  ${CARLA_PYTHON_API_SOURCE_PATH}/TrafficManager.cpp
  ${CARLA_PYTHON_API_SOURCE_PATH}/Weather.cpp
  ${CARLA_PYTHON_API_SOURCE_PATH}/World.cpp
)

if (ENABLE_OSM2ODR)
  list (
    APPEND
    PYTHON_API_SOURCES
    ${CARLA_PYTHON_API_SOURCE_PATH}/OSM2ODR.cpp
  )
endif ()

if (ENABLE_RSS)
  list (
    APPEND
    PYTHON_API_SOURCES
    ${CARLA_PYTHON_API_SOURCE_PATH}/AdRss.cpp
  )
endif ()

set (
  USE_LIMITED_API
  OFF
)

set (
  PYTHON_API_ADD_LIBRARY_ARGS
)

if (USE_LIMITED_API)
  list (
    APPEND
    PYTHON_API_ADD_LIBRARY_ARGS
    USE_SABI ${Python3_VERSION_MAJOR}.${Python3_VERSION_MINOR}
  )
endif ()

list (
  APPEND
  PYTHON_API_ADD_LIBRARY_ARGS
  WITH_SOABI
)

Python3_add_library (
  ${SKBUILD_PROJECT_NAME}
  MODULE
  ${PYTHON_API_ADD_LIBRARY_ARGS}
  ${PYTHON_API_HEADERS}
  ${PYTHON_API_SOURCES}
)

install (
  TARGETS ${SKBUILD_PROJECT_NAME}
  DESTINATION .
)

target_include_directories (
  ${SKBUILD_PROJECT_NAME} SYSTEM PRIVATE
  ${CARLA_PYTHON_API_INCLUDE_PATH}
  $<TARGET_PROPERTY:carla-client,INTERFACE_INCLUDE_DIRECTORIES>
  $<TARGET_PROPERTY:@Boost_asio_TARGET_NAME@,INTERFACE_INCLUDE_DIRECTORIES>
  $<TARGET_PROPERTY:@Boost_python_TARGET_NAME@,INTERFACE_INCLUDE_DIRECTORIES>
  $<TARGET_PROPERTY:@Boost_geometry_TARGET_NAME@,INTERFACE_INCLUDE_DIRECTORIES>
  $<TARGET_PROPERTY:@Boost_gil_TARGET_NAME@,INTERFACE_INCLUDE_DIRECTORIES>
  $<TARGET_PROPERTY:@RecastNavigation_Recast_TARGET_NAME@,INTERFACE_INCLUDE_DIRECTORIES>
  $<TARGET_PROPERTY:@RecastNavigation_Detour_TARGET_NAME@,INTERFACE_INCLUDE_DIRECTORIES>
  $<TARGET_PROPERTY:@RecastNavigation_DetourCrowd_TARGET_NAME@,INTERFACE_INCLUDE_DIRECTORIES>
  $<TARGET_PROPERTY:png_static,INTERFACE_INCLUDE_DIRECTORIES>
  $<TARGET_PROPERTY:zlibstatic,INTERFACE_INCLUDE_DIRECTORIES>
  $<TARGET_PROPERTY:rpc,INTERFACE_INCLUDE_DIRECTORIES>
  @libpng_SOURCE_DIR@ @libpng_BINARY_DIR@
  @zlib_SOURCE_DIR@ @zlib_BINARY_DIR@
)

set (
  PYTHON_API_BUILD_DEFINITIONS
  BOOST_ALL_NO_LIB
  LIBCARLA_WITH_PYTHON_SUPPORT
  @CARLA_COMMON_DEFINITIONS@
  @CARLA_EXCEPTION_DEFINITIONS@
  @CARLA_RTTI_DEFINITIONS@
  $<TARGET_PROPERTY:carla-client,INTERFACE_INCLUDE_COMPILE_DEFINITIONS>
  $<TARGET_PROPERTY:@Boost_asio_TARGET_NAME@,INTERFACE_INCLUDE_COMPILE_DEFINITIONS>
  $<TARGET_PROPERTY:@Boost_python_TARGET_NAME@,INTERFACE_INCLUDE_COMPILE_DEFINITIONS>
  $<TARGET_PROPERTY:@Boost_geometry_TARGET_NAME@,INTERFACE_INCLUDE_COMPILE_DEFINITIONS>
  $<TARGET_PROPERTY:@Boost_gil_TARGET_NAME@,INTERFACE_INCLUDE_COMPILE_DEFINITIONS>
  $<TARGET_PROPERTY:@RecastNavigation_Recast_TARGET_NAME@,INTERFACE_INCLUDE_COMPILE_DEFINITIONS>
  $<TARGET_PROPERTY:@RecastNavigation_Detour_TARGET_NAME@,INTERFACE_INCLUDE_COMPILE_DEFINITIONS>
  $<TARGET_PROPERTY:@RecastNavigation_DetourCrowd_TARGET_NAME@,INTERFACE_INCLUDE_COMPILE_DEFINITIONS>
  $<TARGET_PROPERTY:png_static,INTERFACE_INCLUDE_COMPILE_DEFINITIONS>
  $<TARGET_PROPERTY:zlibstatic,INTERFACE_INCLUDE_COMPILE_DEFINITIONS>
  $<TARGET_PROPERTY:rpc,INTERFACE_INCLUDE_COMPILE_DEFINITIONS>
)

if (WIN32)
  list (
    APPEND
    PYTHON_API_BUILD_DEFINITIONS
    BOOST_PYTHON_STATIC_LIB
  )
endif ()

target_compile_definitions (
  ${SKBUILD_PROJECT_NAME} PRIVATE
  ${PYTHON_API_BUILD_DEFINITIONS}
)

target_link_libraries (
  ${SKBUILD_PROJECT_NAME} PUBLIC
  $<TARGET_FILE:carla-client>
  $<TARGET_FILE:@Boost_python_TARGET_NAME@>
  $<TARGET_FILE:@RecastNavigation_Recast_TARGET_NAME@>
  $<TARGET_FILE:@RecastNavigation_Detour_TARGET_NAME@>
  $<TARGET_FILE:@RecastNavigation_DetourCrowd_TARGET_NAME@>
  $<TARGET_FILE:png_static>
  $<TARGET_FILE:zlibstatic>
  $<TARGET_FILE:rpc>
)
